plugins {
    id 'net.minecrell.gitpatcher' version '0.9.0'
    id 'org.ajoberstar.grgit' version '2.3.0'// apply false
}

patches {
    submodule = 'FernFlower'
    target = file('ForgeFlower')
    patches = file('FernFlower-Patches')
}
applyPatches.dependsOn.remove('updateSubmodules') // We don't want to update the submodule without asking. 

project(':ForgeFlower') {
    apply plugin: 'org.ajoberstar.grgit'
    apply from: rootProject.file('fernflower.gradle')
    compileJava.dependsOn rootProject.tasks.applyPatches
}

project(':ForgeFlower9') {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    sourceCompatibility = targetCompatibility = 9
    group = 'net.minecraftforge'
    
    sourceSets {
        java9.java.srcDirs = ['../ForgeFlower/java9']
    }
    
    eclipse {
        project {
            name 'FernFlower9'
            linkedResource name: 'java9', type: '2', location: project(':ForgeFlower').file('java9').getAbsolutePath()
        }
        jdt {
            sourceCompatibility = targetCompatibility = 9
        }
    }
    
    dependencies {
        compile project(':ForgeFlower')
    }
    tasks.withType(JavaCompile) {
        options.encoding = 'utf-8'
        sourceCompatibility = 9
        targetCompatibility = 9
        options.compilerArgs.addAll(['--release', '9'])
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.8.1'
}

task eclipse() {
    dependsOn('applyPatches', 'ForgeFlower:eclipse', 'ForgeFlower9:eclipse')
}

if (!JavaVersion.current().java9Compatible) {
    println("You must build this with JDK 9")
    System.exit(1)
}
